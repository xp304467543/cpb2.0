// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "com.tencent.bugly:tinker-support:1.1.5"
//        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/zchu/maven/' }
        maven { url "https://dl.bintray.com/thelasterstar/maven/" }
        maven { url "https://repo.eclipse.org/content/repositories/paho-snapshots/" }
        flatDir {
            dirs 'libs'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def getAarDirPath() {
    return projectDir.getPath() + "/socialgo/libs"
}

/**
 * 获取发布版本信息
 */
def getPublishProperty() {
    Properties buildProps = new Properties()
    buildProps.load(new FileInputStream(file('publish.properties')))
    return buildProps
}

/**
 * 获取VersionCode
 */
def getVersionCode() {
    def versionCodeFile = file('version.properties')
    if (versionCodeFile.canRead()) {
        Properties versionCodeProps = new Properties()
        versionCodeProps.load(new FileInputStream(versionCodeFile))
        def versionCode = versionCodeProps['VERSION_CODE'].toInteger()
        // 获取当前任务名
        def runTasks = gradle.startParameter.taskNames
        // 只有在打alpha和beta的正式包的时候进行versionCode的自增操作
        for (task in runTasks) {
            println task
        }
        if (':app:assembleAlphaRelease' in runTasks
                || ':app:assembleAlphaDebug' in runTasks
                || ':app:assembleBetaRelease' in runTasks) {
            // 获取version_code.properties文件的VERSION_CODE的值并加一
            versionCodeProps['VERSION_CODE'] = (++versionCode).toString()
            // 保存versionCode的值到文件中
            versionCodeProps.store(versionCodeFile.newWriter(), null)
        }
        return versionCode
    } else {
        // version_code.properties文件不存在
        throw new GradleException("Could not find verison_code.properties file !!")
    }
}
